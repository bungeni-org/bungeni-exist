<?xml version="1.0" encoding="UTF-8"?>
<xsl:stylesheet xmlns="http://www.w3.org/1999/xhtml" xmlns:xsl="http://www.w3.org/1999/XSL/Transform" xmlns:an="http://www.akomantoso.org/1.0" xmlns:xs="http://www.w3.org/2001/XMLSchema" xmlns:fo="http://www.w3.org/1999/XSL/Format" xmlns:bu="http://portal.bungeni.org/1.0/" version="2.0">
    <xsl:output method="xml" version="2.0" indent="yes"/>
    <xsl:include href="config.xsl"/>
    <xsl:import href="xhtml2fo.xsl"/>
    <!--xsl:include href="xhtml2fo.xsl" /-->
    <xsl:template match="doc">
        <xsl:variable name="doc-name" select="bu:ontology/bu:legislativeItem/bu:shortName"/>
        <xsl:variable name="doc-subject" select="concat('Parliamentary ',bu:ontology/bu:document/@type,' from Bungeni')"/>
        <xsl:variable name="doc-keywords" select="concat('Parliamentary ',bu:ontology/bu:document/@type,',',bu:ontology/bu:bungeni/bu:country)"/>
        <fo:root>
            <fo:layout-master-set>
                <fo:simple-page-master page-height="297mm" page-width="210mm" margin="5mm 25mm 10mm 25mm" master-name="parl-doc">
                    <fo:region-body margin="10mm 0mm 20mm 0mm"/>
                    <fo:region-after margin="10mm 0mm 10mm 0mm"/>
                </fo:simple-page-master>
            </fo:layout-master-set>
            <fo:declarations>
                <x:xmpmeta xmlns:x="adobe:ns:meta/">
                    <rdf:RDF xmlns:rdf="http://www.w3.org/1999/02/22-rdf-syntax-ns#">
                        <rdf:Description xmlns:dc="http://purl.org/dc/elements/1.1/" rdf:about="">
                            <!-- Dublin Core properties go here -->
                            <dc:title>
                                <xsl:value-of select="$doc-name"/>
                            </dc:title>
                            <dc:creator>Bungeni eXist Portal</dc:creator>
                            <dc:description>
                                <xsl:value-of select="$doc-subject"/>
                            </dc:description>
                        </rdf:Description>
                        <!-- 
                              !+FIX_THIS_TODO Add metadata properly for Keywords,
                              not working properly at the moment. Current guide
                              http://xmlgraphics.apache.org/fop/0.95/metadata.html
                        -->
                        <pdf:Keywords xmlns:pdf="http://ns.adobe.com/pdf/1.3/">
                            <pdf:Keywords>
                                <xsl:value-of select="$doc-keywords"/>
                            </pdf:Keywords>
                        </pdf:Keywords>
                        <rdf:Description xmlns:xmp="http://ns.adobe.com/xap/1.0/" rdf:about="">
                            <!-- XMP properties go here -->
                            <xmp:CreatorTool>eXist XML Database with xsl-fo module.</xmp:CreatorTool>
                        </rdf:Description>
                    </rdf:RDF>
                </x:xmpmeta>
            </fo:declarations>
            <fo:page-sequence master-reference="parl-doc">  
                <!-- THIS THE FOOTER SECTION -->
                <fo:static-content flow-name="xsl-region-after">
                    <fo:list-block font-family="'Times New Roman', san-serif" font-size="10pt" provisional-distance-between-starts="5in" provisional-label-separation="0in">
                        <fo:list-item>
                            <fo:list-item-label>
                                <fo:block>
                                    <fo:external-graphic scaling="uniform" src="http://192.168.0.25:8180/exist/apps/framework/assets/bungeni/images/empowered.png" content-height="0.7cm" vertical-align="middle"/>
                                </fo:block><!-- Footer left -->
                            </fo:list-item-label>
                            <fo:list-item-body>
                                <fo:block text-align="end" text-altitude="0pt" font-size="10pt">Page <fo:page-number/> <!-- Footer Right -->
                                </fo:block>
                            </fo:list-item-body>
                        </fo:list-item>
                    </fo:list-block>
                </fo:static-content> 
                <!-- THIS IS THE MAIN BODY -->
                <fo:flow flow-name="xsl-region-body">
                    <fo:block text-align="center" font-size="12pt" font-family="'Times New Roman', san-serif">KENYA PARLIAMENT</fo:block>
                    <fo:block text-align="center">
                        <fo:external-graphic scaling="uniform" src="http://192.168.0.25:8180/exist/apps/framework/assets/bungeni/images/bungeni-logo.png" content-width="1cm" content-height="1cm" vertical-align="middle"/>
                    </fo:block>
                    <xsl:apply-templates select="bu:ontology"/>
                </fo:flow>
            </fo:page-sequence>
        </fo:root>
    </xsl:template>
    <xsl:template match="bu:ontology">
        <xsl:apply-templates mode="title"/>
        <xsl:apply-templates mode="itemno"/>
        <xsl:apply-templates mode="sponsor"/>
        <xsl:apply-templates mode="sponsors"/>
        <xsl:apply-templates mode="status"/>
        <xsl:apply-templates mode="body"/>
        <xsl:apply-templates mode="profile"/>
    </xsl:template>
    <xsl:template mode="title" match="bu:legislativeItem/bu:shortName">
        <fo:block space-before="10pt" font-size="10pt" text-align="center" text-transform="uppercase" font-family="'Times New Roman', san-serif">
            <xsl:value-of select="."/>
        </fo:block>
    </xsl:template>
    <xsl:template mode="itemno" match="bu:legislativeItem/bu:itemNumber">
        <fo:block font-family="'Times New Roman', san-serif" space-before="10pt" font-size="10pt" text-align="center">
            <fo:inline>
                <xsl:value-of select="concat(upper-case(substring(//bu:document/@type, 1, 1)), substring(//bu:document/@type, 2))"/>
            </fo:inline> Number: <xsl:value-of select="."/>
        </fo:block>
    </xsl:template>
    <xsl:template mode="sponsor" match="bu:legislativeItem/bu:owner">
        <xsl:variable name="sponsor-link" select="concat('http://localhost:8180/exist/apps/framework/member?uri=',@href)"/>
        <fo:block font-family="'Times New Roman', san-serif" space-before="8pt" font-size="10pt" text-align="center">
            <fo:inline>Primary Sponsor: </fo:inline>
            <fo:basic-link external-destination="url({$sponsor-link})" color="blue" text-decoration="underline">
                <xsl:value-of select="@showAs"/>
            </fo:basic-link>
        </fo:block>
    </xsl:template>
    <xsl:template mode="sponsors" match="bu:signatories">
        <fo:block font-family="'Times New Roman', san-serif" space-before="8pt" font-size="10pt" text-align="center">
            <xsl:choose>               
                <!-- check whether we have signatories or not -->
                <xsl:when test="bu:signatory">
                    Sponsors: (                     
                    <xsl:for-each select="bu:signatory">
                        <xsl:variable name="sponsors-links" select="concat('http://localhost:8180/exist/apps/framework/member?uri=',@href)"/>
                        <fo:basic-link external-destination="url({$sponsors-links})" color="blue" text-decoration="underline">
                            <xsl:value-of select="@showAs"/>
                        </fo:basic-link>
                        <fo:inline padding-right="5pt">
                            <xsl:if test="position() &lt; last()">,</xsl:if>
                        </fo:inline>
                    </xsl:for-each>
                    )                        
                </xsl:when>
                <xsl:otherwise>
                    No sponsors
                </xsl:otherwise>
            </xsl:choose>
        </fo:block>
    </xsl:template>
    <xsl:template mode="status" match="bu:legislativeItem/bu:status">
        <fo:block font-family="'Times New Roman', san-serif" space-before="8pt" font-size="10pt" text-align="center">
            Last Event: <fo:inline font-weight="bold" padding-right="15pt">
                <xsl:value-of select="."/>
            </fo:inline> Date: <fo:inline font-weight="bold">
                <xsl:value-of select="format-dateTime(//bu:legislativeItem/bu:statusDate,$datetime-format,'en',(),())"/>
            </fo:inline>
        </fo:block>
    </xsl:template>     
    
    <!-- 
        !#FIX_THIS (21 Nov 2011)
        Currently <apply-templates> strips pre-existing xhtml tags and renders 
        text only. Ideally should preserve xhtml formatting as the portal displays
    -->
    <xsl:template mode="body" match="bu:legislativeItem/bu:body">
        <!-- need fo:block-container? or normal fo:block -->
        <xsl:variable name="need-block-container">
            <xsl:call-template name="need-block-container"/>
        </xsl:variable>
        <xsl:choose>
            <xsl:when test="$need-block-container = 'true'">
                <fo:block-container font-family="'Times New Roman'" font-size="10pt">
                    <xsl:if test="@dir">
                        <xsl:attribute name="writing-mode">
                            <xsl:choose>
                                <xsl:when test="@dir = 'rtl'">rl-tb</xsl:when>
                                <xsl:otherwise>lr-tb</xsl:otherwise>
                            </xsl:choose>
                        </xsl:attribute>
                    </xsl:if>
                    <xsl:call-template name="process-common-attributes"/>
                    <fo:block start-indent="0pt" end-indent="0pt" font-family="'Times New Roman'" font-size="10pt">
                        <xsl:apply-templates/>
                    </fo:block>
                </fo:block-container>
            </xsl:when>
            <xsl:otherwise>
                <!-- normal block -->
                <fo:block font-family="'Times New Roman'" font-size="10pt">
                    <xsl:call-template name="process-common-attributes"/>
                    <xsl:apply-templates/>
                </fo:block>
            </xsl:otherwise>
        </xsl:choose>
    </xsl:template>
    <xsl:template mode="profile" match="bu:legislativeItem">
        <fo:list-block font-family="'Times New Roman', san-serif" space-before="8pt" font-size="10pt">
            <fo:list-item>
                <fo:list-item-label>
                    <fo:block>.</fo:block>
                </fo:list-item-label>
                <fo:list-item-body>
                    <fo:block>Document ID: <xsl:value-of select="bu:registryNumber"/>
                    </fo:block>
                </fo:list-item-body>
            </fo:list-item>
            <fo:list-item>
                <fo:list-item-label>
                    <fo:block>.</fo:block>
                </fo:list-item-label>
                <fo:list-item-body>
                    <fo:block>Parliament: <xsl:value-of select="preceding-sibling::bu:bungeni/bu:parliament/@href"/>
                    </fo:block>
                </fo:list-item-body>
            </fo:list-item>
            <fo:list-item>
                <fo:list-item-label>
                    <fo:block>.</fo:block>
                </fo:list-item-label>
                <fo:list-item-body>
                    <fo:block>Session Year: <xsl:value-of select="substring-before(preceding-sibling::bu:bungeni/bu:parliament/@date,'-')"/>
                    </fo:block>
                </fo:list-item-body>
            </fo:list-item>
        </fo:list-block>
    </xsl:template>
</xsl:stylesheet>